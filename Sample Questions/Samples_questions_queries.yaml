Question templates using spatial facets:
  Object - Location/Footprint:
    "Where is the 'Bangs' gauging station located within the catchment area? [What are the coordinates] " : |
        PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
        PREFIX geo: <http://www.opengis.net/ont/geosparql#>
        PREFIX schema: <https://schema.org/>

        SELECT ?geomObj ?easting ?northing ?elevation
        WHERE { GRAPH <http://hydroturtle/LamahCE>
                {# Find the sensor with the name "Bangs"
                ?sensor schema:name "Bangs" ;
                        geo:hasGeometry ?geom .
                
                # Get the WKT representation
                ?geom geo:asWKT ?geomObj.
                
                # Extract coordinates using Virtuoso spatial functions
                BIND (bif:st_x(?geomObj) AS ?easting)   # Longitude
                BIND (bif:st_y(?geomObj) AS ?northing)  # Latitude
                BIND (bif:st_z(?geomObj) AS ?elevation) # Elevation
        }}

    "Where are the gauging stations located within the catchment areas (three catchments) for maximum annual precipitation in 2015?"  : |

        PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
        PREFIX sosa: <http://www.w3.org/ns/sosa/>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
        PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
        PREFIX qudt: <https://qudt.org/schema/qudt/>
        PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
        PREFIX unit: <https://qudt.org/vocab/unit/>
        PREFIX schema: <https://schema.org/>
        PREFIX locn: <http://www.w3.org/ns/locn#>
        PREFIX geo: <http://www.opengis.net/ont/geosparql#>

        SELECT ?stationName ?easting ?northing (SUM(?precipitation) AS ?totalPrecipitation)
        WHERE { GRAPH <http://hydroturtle/LamahCE>
                {# Find the observations with precipitation[envthes:30106] (flow rate for this time - envthes:21242) 
                ?observation rdf:type sosa:Observation ;
                        sosa:observedProperty envthes:30106;
                        sosa:madeBySensor ?station ;
                        sosa:resultTime ?resultTime ;
                        sosa:hasResult ?result .

                #Filter results of 2015
                FILTER(YEAR(?resultTime) = 2015)

                # Access the blank node for QuantityValue
                ?result rdf:type qudt:QuantityValue ;
                        qudt:numericValue ?precipitation.
                        #qudt:unit unit:M3-PER-SEC.

                # Retrieve gauging station and location details
                ?station rdf:type sosa:Sensor ;
                        schema:name ?stationName ;
                        geo:hasGeometry ?locationGeometry .

                ## Get the WKT representation
                ?locationGeometry geo:asWKT ?geomObj.

                
                # Extract coordinates using Virtuoso spatial functions
                BIND (bif:st_x(?geomObj) AS ?easting)   # Longitude
                BIND (bif:st_y(?geomObj) AS ?northing)  # Latitude

        }}

        # Group by station to sum precipitation values (this one is for flow rate for now) for each station in 2015
        GROUP BY ?stationName ?easting ?northing
        ORDER BY DESC(?totalPrecipitation)
        LIMIT 3

    "What re are the largest five catchment areas in this dataset?" : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX schema: <https://schema.org/>
      PREFIX geo: <http://www.opengis.net/ont/geosparql#>

      SELECT ?stationName ?catchment ?catchmentArea 
      WHERE { GRAPH <http://hydroturtle/LamahCE>
      {
            #find the station name 
            ?sensor schema:name ?stationName;
                          n4e_hyd:monitorsCatchment ?catchment;
                          geo:hasGeometry ?locationGeometry .

            # Find the catchment area
            ?catchment n4e_hyd:catchment_area_calculated ?catchmentArea.
      }}
      # take the largest catchment area.  
      ORDER BY DESC(?catchmentArea)
      LIMIT 5


  Object - Boundary:
    "Which catchments share a boundary with the catchment that recorded the highest total precipitation between 2012 and 2015 (inclusive), and what are their boundaries?": |
 
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX geo: <http://www.opengis.net/ont/geosparql#>


      SELECT ?neighbourCatchment ?catchment ?maxPrecipitation ?time ?geomObjCatNeig
      WHERE { GRAPH <http://hydroturtle/LamahCE>
          {
          # Find the catchment with the highest flow rate between 2012-2016
          {

              SELECT ?catchment ?geomObjCat (MAX(?precipitation) AS ?maxPrecipitation) ?time
              WHERE {
                  # Find the observations with precipitation[envthes:30106]
                  ?observation rdf:type sosa:Observation ;
                      sosa:observedProperty envthes:30106;
                      sosa:madeBySensor ?sensor ;
                      sosa:resultTime ?time ;
                      sosa:hasResult ?result.

                  ## take the flow rate value
                  ?result qudt:numericValue ?precipitation.


                  # Take the catchment geometry as WKT
                  ?sensor n4e_hyd:monitorsCatchment ?catchment .
                  ?catchment geo:hasGeometry ?catchmentGeometry.
                  ?catchmentGeometry geo:asWKT ?geomObjCat.
                
                BIND(YEAR(?time) as ?year)

                FILTER (?year>= 2012 && ?year <= 2015)

      }
              ORDER BY DESC(?maxPrecipitation)
              LIMIT 1

      }

                  #from here this should try to take 
                  ?neighbourCatchment rdf:type envthes:30212 ;
                                      geo:hasGeometry ?neighbourCatchmentGeom .

                  ?neighbourCatchmentGeom geo:asWKT ?geomObjCatNeig .


                  FILTER (?neighbourCatchment != ?catchment) # Exclude the max flow catchment
                  FILTER bif:st_intersects(?geomObjCat, ?geomObjCatNeig, 0) # 0 distance means touching borders

              }}

    "What are the names of the gauging stations whose catchment areas share a boundary with the catchment of the 'Garsella' gauging station?": |
        PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
        PREFIX sosa: <http://www.w3.org/ns/sosa/>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
        PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
        PREFIX schema: <https://schema.org/>
        PREFIX geo: <http://www.opengis.net/ont/geosparql#>

        SELECT ?neighbourCatchment ?neighbourSensorName ?catchmentGiven 
        WHERE { GRAPH <http://hydroturtle/LamahCE>
              {
                # Find the catchment with the highest flow rate between 2012-2016
                {
                SELECT ?catchmentGiven ?geomObjCatGiven
                WHERE {
                        # Find the sensor with the name "Garsella" and catchment associated with it
                        ?sensorGiven rdf:type sosa:Sensor;
                                schema:name "Garsella";
                                n4e_hyd:monitorsCatchment ?catchmentGiven.

                        # take the catchment geomatery of the particular sensor 
                        ?catchmentGiven geo:hasGeometry ?catchmentGivenGeometry.
                        ?catchmentGivenGeometry geo:asWKT ?geomObjCatGiven.
        }
        }

                        #Take the neighbouring catchements. 
                        ?neighbourCatchment rdf:type envthes:30212 ;
                                        geo:hasGeometry ?neighbourCatchmentGeom .

                        ?neighbourSensor rdf:type sosa:Sensor;
                                        schema:name ?neighbourSensorName;
                                        n4e_hyd:monitorsCatchment ?neighbourCatchment.

                        ?neighbourCatchmentGeom geo:asWKT ?geomObjCatNeig.

                        FILTER (?neighbourCatchment != ?catchmentGiven) # exclude the given catchment from the selection 
                        FILTER bif:st_intersects(?geomObjCatGiven, ?geomObjCatNeig, 0) # 0 distance means touching borders

                }}


  Object - Spatial Properties:
    "What is the surface area of the largest catchment in the dataset?" : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX schema: <https://schema.org/>

      SELECT ?guagingStationName ?catchment ?catchmentArea
      WHERE { GRAPH <http://hydroturtle/LamahCE>
        {
          # take the catchement areas of catchements (envthes:30212 is the catchement class)
          ?catchment rdf:type envthes:30212 ;
                  n4e_hyd:catchment_area_calculated ?catchmentArea. 

          # find the name of the sensor related with catchement
          ?sensor n4e_hyd:monitorsCatchment ?catchment;
                  schema:name ?guagingStationName. 
      }
      }
      ORDER BY DESC(?catchmentArea)
      LIMIT 1

    
    "What is the average annual precipitation in Catchment 1 (n4e_hyd:catchment_1)during the period from 2012 to 2015, inclusive?" : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX schema: <https://schema.org/>

      SELECT ?guagingStationName (AVG(?yearlyTolalPrecipitation) AS ?averageAnnualPrecipitation) ?unit
      WHERE { GRAPH <http://hydroturtle/LamahCE>
        {
          {
              SELECT ?resultYear (SUM(?precipitation) AS ?yearlyTolalPrecipitation) ?guagingStationName ?unit
              WHERE {
                  
                  # Find the observations with precipitation[envthes:30106] (flow rate for this time - envthes:21242) - 
                  # precipitation can be written as directly associalted with catchment as observerd property 
                  ?observation rdf:type sosa:Observation ;
                      sosa:observedProperty envthes:30106 ;
                      sosa:madeBySensor ?sensor ;
                      sosa:resultTime ?resultTime ;
                      sosa:hasResult ?result.

                  # Access the blank node to retrieve the precipitation value 
                  ?result qudt:numericValue ?precipitation;
                          qudt:unit ?unit.

                  # find catchment related with sensor 
                  ?sensor n4e_hyd:monitorsCatchment n4e_hyd:catchment_1;
                          schema:name ?guagingStationName

                  # Extract the year directly in the FILTER and assign to ?year 
                  BIND (YEAR(?resultTime) AS ?resultYear)
                  FILTER (?resultYear >= 2012 && ?resultYear <= 2015)
              }
              GROUP BY ?resultYear ?guagingStationName ?unit
          }
      }}


    "What are the average annual flow rates at Gauging Station 1 (n4e_hyd:sensor_1) for each year from 2012 to 2015, inclusive?" : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX schema: <https://schema.org/>

      SELECT ?resultYear (AVG(?flowRate) AS ?yearlyAvgFlowrate) ?guagingStationName ?unit
      WHERE { GRAPH <http://hydroturtle/LamahCE> 
            {
                  # Find the observations with precipitation[envthes:30106] (flow rate for this time - envthes:21242) - 
                  # precipitation can be written as directly associalted with catchment as observerd property 
                  ?observation rdf:type sosa:Observation ;
                      sosa:observedProperty envthes:21242 ;
                      sosa:madeBySensor n4e_hyd:sensor_1 ;
                      sosa:resultTime ?resultTime ;
                      sosa:hasResult ?result.

                  ## # Access the blank node to retrieve the precipitation value [for this case used flow rate due to uploaded data for the moment to triple store ]
                  ?result qudt:numericValue ?flowRate;
                          qudt:unit  ?unit.

                  # find catchment related with sensor 
                  n4e_hyd:sensor_1 schema:name ?guagingStationName.

                  # Extract the year directly in the FILTER and assign to ?year [this query is written between 2012 to 2015 for the moment data is uploaded]
                  BIND (YEAR(?resultTime) AS ?resultYear)
                  FILTER (?resultYear >= 2012 && ?resultYear <= 2015)
              }
              }
              GROUP BY ?resultYear ?guagingStationName ?unit

    "What is the elevation of the 'Beschling' gauging station?" : |
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX schema: <https://schema.org/>
      PREFIX geo: <http://www.opengis.net/ont/geosparql#>

      SELECT ?easting ?northing ?elevation ?geomObj
      FROM <http://hydroturtle/LamahCE>
      WHERE { GRAPH <http://hydroturtle/LamahCE> 
          {

          # Find the sensor with the name "Beschling"
          ?sensor schema:name "Beschling" ;  
                  rdf:type sosa:Sensor;
                  geo:hasGeometry ?geom .
          
          # Get the WKT representation
          ?geom geo:asWKT ?geomObj .
          
          # Extract coordinates using Virtuoso spatial functions
          BIND (bif:st_x(?geomObj) AS ?easting)   # Longitude
          BIND (bif:st_y(?geomObj) AS ?northing)  # Latitude
          BIND (bif:st_z(?geomObj) AS ?elevation) # Elevation
      }
      }

    
    "What is the highest average monthly evaporation recorded at any catchment between 2000 and 2009 (inclusive), and what is the name of the associated gauging station? evaporation is envthes:21299" : |

        PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
        PREFIX sosa: <http://www.w3.org/ns/sosa/>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
        PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
        PREFIX qudt: <https://qudt.org/schema/qudt/>
        PREFIX unit: <https://qudt.org/vocab/unit/>
        PREFIX schema: <https://schema.org/>

        SELECT ?resultMonth (SUM(?evapotranspiration) AS ?monthlyTolalEvapotranspiration) ?unit ?guagingStationName
        WHERE { GRAPH <http://hydroturtle/LamahCE> 
              {
                # Find the observations with evapotranspiration[envthes:21299] 
                # precipitation can be written as directly associalted with catchment as observerd property 
                ?observation rdf:type sosa:Observation ;
                    sosa:observedProperty envthes:21299 ;
                    sosa:madeBySensor ?sensor ;
                    sosa:resultTime ?resultTime ;
                    sosa:hasResult ?result.

                ## # Access the blank node to retrieve the precipitation value [for this case used flow rate due to uploaded data for the moment to triple store ]
                ?result qudt:numericValue ?evapotranspiration;
                        qudt:unit  ?unit.

                # find catchment related with sensor 
                ?sensor n4e_hyd:monitorsCatchment ?catchment;
                        schema:name ?guagingStationName

                # Extract the year and month assigned to ?resultYear and ?resultMonth directly 
                BIND (YEAR(?resultTime) AS ?resultYear)
                BIND (MONTH(?resultTime)AS ?resultMonth)

                # Filter and assign to ?year [
                FILTER (?resultYear >= 2000 && ?resultYear <= 2009)
            }
            }
            GROUP BY ?resultMonth ?guagingStationName ?unit
            ORDER BY DESC(?monthlyTolalEvapotranspiration)
            LIMIT 1   # can use limit to see only the maximum

    "What is the all-time minimum and maximum river flow observed at the 'Schwarzach' gauging station?" : |

      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX schema: <https://schema.org/>

      SELECT (MIN(?flowRate) AS ?minRiverFlow) (MAX(?flowRate) AS ?maxRiverFlow) ?unit
      WHERE { GRAPH <http://hydroturtle/LamahCE> 
        {
          # Find the observations with precipitation[envthes:30106] (flow rate for this time - envthes:21242) - 
          # precipitation can be written as directly associalted with catchment as observerd property 
          ?observation rdf:type sosa:Observation ;
              sosa:observedProperty envthes:21242 ;
              sosa:madeBySensor ?sensor ;
              sosa:resultTime ?resultTime ;
              sosa:hasResult ?result.

          ## # Access the blank node to retrieve the precipitation value [for this case used flow rate due to uploaded data for the moment to triple store ]
          ?result qudt:numericValue ?flowRate;
                  qudt:unit  ?unit.

          # find gauging station name 
          ?sensor schema:name "Schwarzach" .   
          }
          }

    "What are the all-time minimum and maximum river flow observed at all the gauging stations?" : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX schema: <https://schema.org/>

      SELECT ?guagingStationName (MIN(?flowRate) AS ?minRiverFlow) (MAX(?flowRate) AS ?maxRiverFlow) ?unit
      WHERE { GRAPH <http://hydroturtle/LamahCE> 
        {
          ?observation rdf:type sosa:Observation ;
              sosa:observedProperty envthes:21242 ; # flow rate - envthes:21242
              sosa:madeBySensor ?sensor ;
              sosa:resultTime ?resultTime ;
              sosa:hasResult ?result.

          ## # Access the blank node to retrieve the precipitation value [for this case, used flow rate due to uploaded data for the moment to triple store ]
          ?result qudt:numericValue ?flowRate;
                  qudt:unit  ?unit.

          # # find gauging station name  
          ?sensor n4e_hyd:monitorsCatchment ?catchment;
                  schema:name ?guagingStationName #"Schwarzach"    ## use directly station name instead of ?guagingStationName variable only particular station data need to be extracted. 
          }
          }
          GROUP BY ?guagingStationName ?unit #can remove GROUP BY - specially, only one gauging station is used. 

    "How many annual average frost days (defined as days with temperatures below 0°C) were recorded at the 'Bangs' gauging station between 2010 and 2020?, Temperature can be taken from n4e_hyd:AirTemperature2mAvg" : |
      #### need to double check the query . answers cannot check some part of the TTL files hasn't upload when I try to upload catchment 1 [Bangs]time series .
      #### Most liketly this is incorrect 

      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX schema: <https://schema.org/>


      SELECT ?year (AVG(?frostDays) AS ?averageAnnualFrostDays)
      WHERE { GRAPH <http://hydroturtle/LamahCE> 
          {
          {
            SELECT ?year (COUNT(?resultDate) AS ?frostDays)
            WHERE {
              # Because the freezing is related to min air temp -  temperature observations at the "Dillingen" station for AirTemperature2mAVG (for this min temp also can be used)
              ?observation rdf:type sosa:Observation ;
                          sosa:observedProperty n4e_hyd:AirTemperature2mAvg ;
                          sosa:madeBySensor ?sensor ;
                          sosa:resultTime ?resultDate ;
                          sosa:hasResult ?result .
              
              # Filter for the "Bangs" station and dates between 2010 and 2020 #only "Bangs" catchment TS is uploaded for now. therefore it used. 
              ?sensor rdf:type sosa:Sensor ;
                      schema:name "Bangs" .

              FILTER (YEAR(?resultDate) >= 2010 && YEAR(?resultDate) <= 2020)
        
              # Get temperature value and check if it's below 0°C
              ?result qudt:numericValue ?temperature .
              FILTER (?temperature < 0)
        
              # Extract the year for grouping
              BIND(YEAR(?resultDate) AS ?year)
            }
            GROUP BY ?year
          }
        }
        }
        GROUP BY ?year
        ORDER BY ?year

  Network - Spatial Relationships:
    "What is the spatial relationship between the 'Bangs' and 'Loruens-Aeule' gauging stations in terms of distance and elevation difference?" : |

        PREFIX sosa: <http://www.w3.org/ns/sosa/>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
        PREFIX schema: <https://schema.org/>
        PREFIX geo: <http://www.opengis.net/ont/geosparql#>

        SELECT ?distance  ?elevDiffBeschlingMinusGarsella ?geomObjBangs ?geomObjLoruensAeule ?elevDiffBeschlingMinusGarsella
        WHERE { GRAPH <http://hydroturtle/LamahCE> 
        {
            # Find the sensor with the name "Bangs"
            ?sensorBeschling schema:name "Bangs" ; 
                    geo:hasGeometry ?geomBangs .
            
            # Get the WKT representation
            ?geomBangs geo:asWKT ?geomObjBangs .
            
            # Extract coordinates using Virtuoso spatial functions
            BIND (bif:st_x(?geomObjBangs) AS ?eastingBangs)   # Longitude
            BIND (bif:st_y(?geomObjBangs) AS ?northingBangs)  # Latitude
            BIND (bif:st_z(?geomObjBangs) AS ?elevationBangs) # Elevation


            # Find the sensor with the name "Loruens-Aeule"
            ?sensorLoruensAeule schema:name "Loruens-Aeule" ; 
                    geo:hasGeometry ?geomLoruensAeule .
            
            # Get the WKT representation
            ?geomLoruensAeule geo:asWKT ?geomObjLoruensAeule .
            
            # Extract coordinates using Virtuoso spatial functions
            BIND (bif:st_x(?geomObjLoruensAeule) AS ?eastingLoruensAeule)   # Longitude
            BIND (bif:st_y(?geomObjLoruensAeule) AS ?northingLoruensAeule)  # Latitude
            BIND (bif:st_z(?geomObjLoruensAeule) AS ?elevationLoruensAeule) # Elevation
            
            # Compute geodesic distance in meters
            BIND (bif:st_distance(?geomObjBangs, ?geomObjLoruensAeule, <http://www.opengis.net/def/uom/OGC/1.0/metre>) AS ?distance)
            BIND ((?elevationBangs-?elevationLoruensAeule) as ?elevDiffBeschlingMinusGarsella)

        }
        }
    "What is the distance between the gauging stations with the highest and lowest elevations in the dataset?" : |
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX schema: <https://schema.org/>
      PREFIX geo: <http://www.opengis.net/ont/geosparql#>

      SELECT ?maxStationName ?maxElevation ?minStationName ?minElevation ?distance
      FROM <http://hydroturtle/LamahCE>
      WHERE {GRAPH <http://hydroturtle/LamahCE> 
          {
          {
              # Get the station with the highest elevation
              SELECT ?maxStationName ?maxElevation ?geomObjMax
              WHERE {
                  ?sensor schema:name ?maxStationName ; 
                          geo:hasGeometry ?geom .
                  ?geom geo:asWKT ?geomObjMax .
                  BIND (bif:st_z(?geomObjMax) AS ?maxElevation) # Elevation
              }
              ORDER BY DESC(?maxElevation)
              LIMIT 1
          }
          
          {
              # Get the station with the lowest elevation
              SELECT ?minStationName ?minElevation ?geomObjMin
              WHERE {
                  ?sensor schema:name ?minStationName ; 
                          geo:hasGeometry ?geom .
                  ?geom geo:asWKT ?geomObjMin .
                  BIND (bif:st_z(?geomObjMin) AS ?minElevation) # Elevation
              }
              ORDER BY ASC(?minElevation)
              LIMIT 1
          }


          # Compute the distance between the highest and lowest stations
          BIND (bif:st_distance(?geomObjMax, ?geomObjMin, <http://www.opengis.net/def/uom/OGC/1.0/metre>) AS ?distance)
      }
      }

    "What are the elevations of the gauging stations within a 10 km radius of the 'Gisingen' gauging station?" : |
        PREFIX sosa: <http://www.w3.org/ns/sosa/>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
        PREFIX unit: <https://qudt.org/vocab/unit/>
        PREFIX schema: <https://schema.org/>
        PREFIX geo: <http://www.opengis.net/ont/geosparql#>

        SELECT ?surroundingGaugingName ?sensorSurrounding  ?elevation ?distance ?northing ?easting
        WHERE { GRAPH <http://hydroturtle/LamahCE>
        {
                # Find the sensor with the name "Gisingen"
                ?sensorGiven a sosa:Sensor;
                                schema:name "Gisingen"; 
                                geo:hasGeometry ?geomGiven .
                
                # Get the WKT representation
                ?geomGiven geo:asWKT ?geomObjGiven .
                
                # Convert WKT into geometry object using Virtuoso functions
                # BIND (bif:st_geomfromtext(?wktGiven) AS ?geomObjGiven)

                
                # Find other sensors 
                ?sensorSurrounding a sosa:Sensor;
                                        schema:name ?surroundingGaugingName; 
                                        geo:hasGeometry ?geomSurrounding .
                
                # Get the WKT representation
                ?geomSurrounding geo:asWKT ?geomObjSurrounding .
                
                # Convert WKT into geometry object using Virtuoso functions
                # BIND (bif:st_geomfromtext(?wktSurrounding) AS ?geomObjSurrounding)

                
                # Extract coordinates using Virtuoso spatial functions
                BIND (bif:st_x(?geomObjSurrounding) AS ?easting)   # Longitude
                BIND (bif:st_y(?geomObjSurrounding) AS ?northing)  # Latitude
                BIND (bif:st_z(?geomObjSurrounding) AS ?elevation) # Elevation

                # Exclude the given gauging station from the selection
                FILTER (?sensorSurrounding != ?sensorGiven)

                # select the surrounding stations 
                FILTER  bif:st_intersects(?geomObjGiven, ?geomObjSurrounding, 0.12 )  # distance need to given in degrees since geomatric objects are in WGS84 coordinates. 
                BIND (bif:st_distance(?geomObjGiven, ?geomObjSurrounding, unit:KiloM ) AS ?distance)
        }
        }



Context-Specific Questions:
  General Spatial Analysis of Catchments:
    "Which catchment has the largest area?" : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX schema: <https://schema.org/>

      SELECT ?guagingStationName ?catchment ?catchmentArea
      WHERE { GRAPH <http://hydroturtle/LamahCE>
          {
          # take the catchement areas of catchements (envthes:30212 is the catchement class)
          ?catchment rdf:type envthes:30212 ;
                  n4e_hyd:catchment_area_calculated ?catchmentArea. 

          # find the name of the sensor related with catchement
          ?sensor n4e_hyd:monitorsCatchment ?catchment;
                  schema:name ?guagingStationName. 
      }
      }
      ORDER BY DESC(?catchmentArea)
      LIMIT 1

    "Which catchment recorded the highest annual precipitation in 2015?" : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX schema: <https://schema.org/>

      SELECT ?catchment (SUM(?precipitation) AS ?totalAnnualPrecipitation) ?unit ?catchmentArea 
      WHERE { GRAPH <http://hydroturtle/LamahCE>
          {
          ?observation rdf:type sosa:Observation ;
                      sosa:observedProperty envthes:30106 ;  # precipitation[envthes:30106]
                      sosa:madeBySensor ?sensor ;
                      sosa:resultTime ?resultTime ;
                      sosa:hasResult ?result.

          # Retrieve the precipitation value
          ?result qudt:numericValue ?precipitation;
                  qudt:unit  ?unit.

          # Find catchment monitored by the sensor
          ?sensor n4e_hyd:monitorsCatchment ?catchment.

          # Find catchment area of the catchment
          ?catchment n4e_hyd:catchment_area_calculated ?catchmentArea

          # Filter for the selected year
          BIND(YEAR(?resultTime) AS ?resultYear)
          FILTER (?resultYear = 2015)
      }
      }
      GROUP BY ?catchment ?catchmentArea ?unit
      ORDER BY DESC(?totalAnnualPrecipitation)  # Sort by highest precipitation
      LIMIT 1  # Select only the catchment with the highest precipitation

    "What is the average atmospheric temperature at 2 meters (n4e_hyd:AirTemperature2mAvg ) above ground level for Catchment 1 (n4e_hyd:catchment_1)during the period from 2010 to 2020?" : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX schema: <https://schema.org/>

      SELECT ?stationName (AVG(?temperatureValue) AS ?averageTemperature)
      WHERE { GRAPH <http://hydroturtle/LamahCE>
          {
          ?observation rdf:type sosa:Observation ;
                      sosa:observedProperty n4e_hyd:AirTemperature2mAvg ;  # AVG Atmospheric temperature at 2m hiehgh 
                      sosa:madeBySensor ?sensor ;
                      sosa:resultTime ?resultTime ;
                      sosa:hasResult ?result.

          # Extract the numeric temperature value
          ?result qudt:numericValue ?temperatureValue;
                  qudt:unit unit:DEG_C.

          # Find sensors monitoring Catchment_1
          ?sensor n4e_hyd:monitorsCatchment n4e_hyd:catchment_1;
                  schema:name ?stationName.

          # Filter for the selected time range
          BIND(YEAR(?resultTime) AS ?resultYear)
          FILTER (?resultYear >= 2010 && ?resultYear <= 2020)
      }
      }

  Temporal and Event-Based:  

    "Which three months experienced the highest precipitation in Austria in 2012?" : |
      # the current query is written for flow rate, in Austria because of limited data uploaded into the triple store. 

      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX esco: <https://ec.europa.eu/esco/lod/static/model.html#>


      SELECT  ?month 
              (AVG(?precipitation) AS ?averageMontlyPrecipitation)
              ?unit 
              (COUNT(?precipitation) AS ?noOfMeasurments) 
              (COUNT(DISTINCT ?station) AS ?numOfStations)
      WHERE { GRAPH <http://hydroturtle/LamahCE>
        {

              # Find the observations with precipitation[envthes:30106] (flow rate for this time - envthes:21242) 
              ?observation rdf:type sosa:Observation ;
                      sosa:observedProperty envthes:30106 ;
                      sosa:madeBySensor ?station ;
                      sosa:resultTime ?resultTime ;
                      sosa:hasResult ?result .
  
              # Access the blank node for QuantityValue
              ?result rdf:type qudt:QuantityValue ;
                      qudt:numericValue ?precipitation;
                      qudt:unit ?unit.

              # Find the gauging stations situated in Germanay
              ?station rdf:type sosa:Sensor ;
                      esco:isoCountryCodeA3 "AUT".    # This should be changed to "DEU" for Germanay. For the moment only 35 stations uploaded. All are from Austria  

              # extract year and month from result time and assign them to seperate variables ?year and ?month
              BIND (YEAR(?resultTime) AS ?year)
              BIND (MONTH(?resultTime) AS ?month)
           
              #Filter results of 2012
              FILTER(?year = 2012)
      }
      }
      # Group by monthly
      GROUP BY ?month ?unit
      ORDER BY DESC(?averageMontlyPrecipitation)
      LIMIT 3


    "In which years did 'OberaBuers (Bruecke L82)' experience the highest number of days with minor flooding, defined as days when river flow exceeded 5 m³/s??   [this question makes much sense if data is available as water level, but in this data set it is not available.] " : |
      PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
      PREFIX sosa: <http://www.w3.org/ns/sosa/>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
      PREFIX qudt: <https://qudt.org/schema/qudt/>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      PREFIX unit: <https://qudt.org/vocab/unit/>
      PREFIX schema: <https://schema.org/>

      SELECT ?year (COUNT(DISTINCT ?date) AS ?floodDays)
      WHERE { GRAPH <http://hydroturtle/LamahCE>
          {
          # Find observations for river flow
          ?observation rdf:type sosa:Observation ;
                      sosa:observedProperty envthes:21242 ;  # River flow
                      sosa:madeBySensor ?sensor ;
                      sosa:resultTime ?resultTime ;
                      sosa:hasResult ?result .

          # Extract river flow value
          ?result rdf:type qudt:QuantityValue ;
                  qudt:numericValue ?flowRate ;
                  qudt:unit unit:M3-PER-SEC .

          # Filter for the location "Buers (Bruecke L82)"
          ?sensor schema:name "Buers (Bruecke L82)".

          # Filter for flow rates exceeding 5 m³/s
          FILTER(?flowRate > 5)

          # Extract year and date from the timestamp 
          BIND(YEAR(?resultTime) AS ?year)
          ## This is particularly for extract and standardize the date from the timestamp 
          BIND(STRDT(CONCAT(STR(YEAR(?resultTime)), "-", STR(MONTH(?resultTime)), "-", STR(DAY(?resultTime))), xsd:date) AS ?date)
      }
      }

      # Group by year to count flood days
      GROUP BY ?year
      ORDER BY DESC(?floodDays)
      LIMIT 1 # Change this to 1 when it only needs to take the year with most of the days of flooding. This provides the top 5 years. 

    "During which five years did the 'Buers (Bruecke L82)' gauging station record the highest number of flood events, based on the assumption that flood events occur when the river flow exceeds 5 m³/s # # in here it is assumed that when the flow is above 5 m3/s then flood events occur. This is incorrect. It should be connected to the water level. However, it is not available in this dataset. " : |
  
        PREFIX sosa: <http://www.w3.org/ns/sosa/>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
        PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
        PREFIX qudt: <https://qudt.org/schema/qudt/>
        PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
        PREFIX unit: <https://qudt.org/vocab/unit/>
        PREFIX schema: <https://schema.org/>

        SELECT ?halfDecade (COUNT(DISTINCT ?date) AS ?floodDays)
        WHERE { GRAPH <http://hydroturtle/LamahCE>
            {
            # Find observations for river flow
            ?observation rdf:type sosa:Observation ;
                        sosa:observedProperty envthes:21242 ;  # River flow
                        sosa:madeBySensor ?sensor ;
                        sosa:resultTime ?resultTime ;
                        sosa:hasResult ?result .

            # Extract river flow value
            ?result rdf:type qudt:QuantityValue ;
                    qudt:numericValue ?flowRate. 

            # Filter for the location "Buers (Bruecke L82)"
            ?sensor schema:name "Buers (Bruecke L82)".

            # Filter for flow rates exceeding 5 m³/s
            FILTER(?flowRate > 5)

            # Extract year and determine the half-decade period
            BIND(YEAR(?resultTime) AS ?year)
            BIND(FLOOR((?year - 1) / 5) * 5 + 1 AS ?halfDecade)  # Computes half-decade periods like 1996-2000, 2001-2005, etc.

            # Extract date from timestamp (only Year-Month-Day, ignoring time)
            BIND(STRDT(CONCAT(STR(YEAR(?resultTime)), "-", STR(MONTH(?resultTime)), "-", STR(DAY(?resultTime))), xsd:date) AS ?date)

        }
        }
        # Group by half-decade and count flood events
        GROUP BY ?halfDecade
        ORDER BY DESC(?floodDays)
        LIMIT 5 

  Specific to Gauging Stations:
    "What is the elevation of the gauging station that recorded the lowest average river flow during the decade 1990 to 1999?" : |

        PREFIX sosa: <http://www.w3.org/ns/sosa/>
        PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
        PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
        PREFIX qudt: <https://qudt.org/schema/qudt/>
        PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
        PREFIX unit: <https://qudt.org/vocab/unit/>
        PREFIX schema: <https://schema.org/>
        PREFIX locn: <http://www.w3.org/ns/locn#>
        PREFIX geo: <http://www.opengis.net/ont/geosparql#>

        SELECT ?gaugingStationName ?elevation (AVG(?flowRate) AS ?avgFlowRate) ?unit
        WHERE { GRAPH <http://hydroturtle/LamahCE>
                {
                # Observations of river flow
                ?observation rdf:type sosa:Observation ;
                        sosa:observedProperty envthes:21242 ;
                        sosa:madeBySensor ?sensor ;
                        sosa:resultTime ?resultTime ;
                        sosa:hasResult ?result.

                # Access flow rate value
                ?result qudt:numericValue ?flowRate;
                        qudt:unit ?unit.

                # Link sensor to gauging station
                ?sensor schema:name ?gaugingStationName;
                        geo:hasGeometry ?geom.

                # Get the WKT representation of the location
                ?geom geo:asWKT ?geomObj.

                # Extract elevation (Z coordinate)
                BIND (bif:st_z(?geomObj) AS ?elevation)

                # Filter data within a relevant time range (e.g., 2012-2013)
                BIND (YEAR(?resultTime) AS ?resultYear)
                FILTER (?resultYear >= 1990 && ?resultYear <= 1999)
        }
        }
        GROUP BY ?gaugingStationName ?elevation ?unit
        ORDER BY ASC(?avgFlowRate)
        LIMIT 1






